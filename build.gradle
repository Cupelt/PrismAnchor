plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.3.0'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    // implementation "org.reflections:reflections:0.9.11"
    
    implementation "com.google.inject:guice:7.0.0"
    implementation 'com.google.inject.extensions:guice-assistedinject:7.0.0'

    compileOnly("org.projectlombok:lombok:1.18.42")
	annotationProcessor("org.projectlombok:lombok:1.18.42")
	
	testCompileOnly("org.projectlombok:lombok:1.18.42")
	testAnnotationProcessor("org.projectlombok:lombok:1.18.42")
}

jar {
    enabled = false
    dependsOn shadowJar
}

shadowJar {
    relocate 'com.google.inject', 'org.cupelt.prismanchor.inject'
    archiveClassifier.set('')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            artifact(tasks.shadowJar)
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'

    group = 'org.cupelt.prismanchor'
    version = findProperty('version') ?: "SNAPSHOT"

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }

    dependencies {
        compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"

        testImplementation 'com.github.seeseemelk:MockBukkit-v1.20:3.9.0'
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
        failOnNoDiscoveredTests = false
    }

    def targetJavaVersion = 21

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.release.set(targetJavaVersion)
    }
}