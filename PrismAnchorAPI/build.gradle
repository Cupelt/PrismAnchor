plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id 'maven-publish'
}

group = 'org.cupelt'
version = '0.0.1-Alpha'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    implementation "org.reflections:reflections:0.9.11"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

jar{
    enabled = false
    finalizedBy shadowJar
}

shadowJar {
    relocate 'org.reflections', 'org.cupelt.prismanchor.reflections'
    archiveClassifier.set('')

//    finalizedBy("copyToRootBuild")
//    finalizedBy("clean")
}

// 서브프로젝트에서 루트로 복사하는 작업 정의
tasks.register("copyToRootBuild", Copy) {
    from(buildDir) // 서브프로젝트의 build 폴더
    into("${rootProject.buildDir}") // 루트 build 폴더에 서브프로젝트 이름으로 복사
}

tasks.named("clean").configure {
    delete "${rootProject.buildDir}"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
