plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id 'maven-publish'
}

group = 'org.cupelt'
version = '0.0.1-Alpha'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    implementation "org.reflections:reflections:0.9.11"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

jar{
    enabled = false
    finalizedBy shadowJar
}

shadowJar {
    relocate 'org.reflections', 'org.cupelt.prismanchor.reflections'
    archiveClassifier.set('')

    finalizedBy('copyToMainBuild')
}

tasks.register('copyToMainBuild', Copy) {
    dependsOn(shadowJar) // shadowJar 작업이 먼저 실행되도록 설정
    from("$buildDir/libs") // shadow JAR 파일 경로
    include("*.jar") // JAR 파일만 복사
    into("${rootProject.buildDir}/libs") // 메인 프로젝트의 빌드 폴더 내 경로
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
